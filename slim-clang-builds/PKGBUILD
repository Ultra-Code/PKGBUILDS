# Maintainer: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

#INFO: https://llvm.org/docs/CMake.html
#INFO: https://llvm.org/docs/AdvancedBuilds.html
#INFO: https://llvm.org/docs/BuildingADistribution.html
#INFO: https://libcxx.llvm.org/BuildingLibcxx.html

pkgname=(llvm-minimal-git llvm-libs-minimal-git clang-minimal-git)
pkgver=19.0.0_r497085.b6a8f5486b
pkgrel=1
pkgdesc='C language family frontend for LLVM (trimmed down git version)'
url="https://llvm.org/"
pkgbase=llvm-minimal-git
arch=('x86_64')
license=('custom:Apache 2.0 with LLVM Exception')
makedepends=(git ninja cmake clang mold libffi libedit ncurses libxml2 libxcrypt python python-setuptools)
# b361b5369ed4
source=("git+https://github.com/llvm/llvm-project.git"
  fix-missing-include-when-building-clang-with-modules.patch)
b2sums=('SKIP' 'SKIP')

# explicitly disable lto & debug to reduce number of build hangs , resources needed and runtime issues
options=(!lto !debug)

_major_ver=19

_get_distribution_components() {
  local target

  ninja -t targets | grep -Po 'install-\K.*(?=-stripped)' | while read -r target; do
    case $target in
    # Must come before LLVM* to ensure it isn't skipped
    LLVM) ;;
    # Build libraries needed for clang-tblgen
    LLVMDemangle | LLVMSupport | LLVMTableGen) ;;
    #matched targets with continue statements are skipped
    distribution | llvm-libraries) continue ;;
    # exclude llvm-exegesis (doesn't seem useful without libpfm)
    llvm-exegesis) continue ;;
    # don't build functionality conflicting with llvm-libs
    LLVMgold) continue ;;
    # exclude static libraries
    LLVM*) continue ;;
    clang-libraries | clang-tidy-headers) continue ;;
    # Build shared libraries
    clang | clangd | clang-*) ;;
    # Skip all else
    clang* | findAllSymbols) continue ;;
    # Anything not covered above is included
    *) ;;
    esac
    echo $target
  done
}

pkgver() {
  cd llvm-project/cmake/Modules

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
    'BEGIN { ORS="." ; i=0 } \
             /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
             END { print "\n" }' \
    LLVMVersion.cmake)_r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  echo "${_pkgver}"
}

prepare() {
  # fix module build error
  pushd "$srcdir"/llvm-project
  git apply ../fix-missing-include-when-building-clang-with-modules.patch
  popd
}

build() {
  CFLAGS=${CFLAGS}
  # https://reviews.llvm.org/D142279
  # GLIBCXX_* seem to enable assertions
  # Remove all occurances of -Wp,-D_GLIBCXX_ASSERTIONS
  CXXFLAGS=${CXXFLAGS//-Wp,-D_GLIBCXX_ASSERTIONS/""}
  LDFLAGS=${LDFLAGS}

  #Test have C|XXFLAGS exported at the top be for use if it causes it load the user specific ones also
  local cmake_args=(
    -GNinja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLLVM_ABI_BREAKING_CHECKS=FORCE_OFF
    -DLLVM_BINUTILS_INCDIR=/usr/include
    -DCMAKE_C_COMPILER=clang
    -DCMAKE_CXX_COMPILER=clang++
    -DLLVM_PARALLEL_LINK_JOBS=6
    -DLLVM_PARALLEL_COMPILE_JOBS=6
    -DLLVM_HOST_TRIPLE="${CHOST}"
    -DCMAKE_C_FLAGS_RELEASE="${CFLAGS} -Wno-modules-import-nested-redundant"
    -DCMAKE_CXX_FLAGS_RELEASE="${CXXFLAGS}"
    -DLLVM_ENABLE_PROJECTS="clang;clang-tools-extra"
    -DLLVM_ENABLE_RUNTIMES="libcxx;libcxxabi;compiler-rt"
    -DLLVM_ENABLE_LIBCXX=YES
    -DLLVM_ENABLE_MODULES=YES
    -DLIBCXX_INSTALL_MODULES=YES
    -DLLVM_ENABLE_UNWIND_TABLES=NO
    -DLIBCXXABI_USE_LLVM_UNWINDER=NO
    -DLLVM_TARGETS_TO_BUILD=X86
    -DLLVM_USE_LINKER=mold
    -DLLVM_ENABLE_LTO=NO #Thin increases link time drastically
    -DLLVM_INSTALL_UTILS=NO
    -DLLVM_BUILD_LLVM_DYLIB=YES
    -DLLVM_LINK_LLVM_DYLIB=YES
    -DCLANG_LINK_CLANG_DYLIB=YES
    -DLLVM_BUILD_BENCHMARKS=NO
    -DLLVM_INCLUDE_BENCHMARKS=NO
    -DLLVM_BUILD_DOCS=NO
    -DLLVM_INCLUDE_DOCS=NO
    -DLLVM_BUILD_EXAMPLES=NO
    -DLLVM_INCLUDE_EXAMPLES=NO
    -DLLVM_BUILD_TESTS=NO
    -DLLVM_INCLUDE_TESTS=NO
    -DLLVM_ENABLE_BINDINGS=NO
  )
  cmake -B _build -S "$srcdir"/llvm-project/llvm "${cmake_args[@]}" -Wno-dev

  pushd "$srcdir"/_build
  local distribution_components=$(_get_distribution_components | paste -sd\;)
  popd
  test -n "$distribution_components"
  cmake_args+=(-DLLVM_DISTRIBUTION_COMPONENTS="$distribution_components")

  cmake -B _build -S "$srcdir"/llvm-project/llvm "${cmake_args[@]}" -Wno-dev

  ninja -C _build
}

package_llvm-minimal-git() {
  pkgdesc="Collection of modular and reusable compiler and toolchain technologies, trimmed down git version"
  depends=(llvm-libs-minimal-git="$pkgver-$pkgrel")
  provides=('llvm')
  conflicts=('llvm')
  optdepends=('python: for using lit (LLVM Integrated Tester)'
    'python-setuptools: for using lit'
  )
  rm -rf "$pkgdir" && rm -rf "$srcdir"/{clang,llvm-libs}

  DESTDIR="$pkgdir" ninja -C _build install-distribution-stripped

  # Include lit for running lit-based tests in other projects
  pushd "$srcdir"/llvm-project/llvm/utils/lit
  # -O1 ensures the python files for lit will be optimized
  python3 setup.py install --root="$pkgdir" -O1
  popd

  # Remove symlinks to prevent conflict with repo llvm-libs
  rm -f "$pkgdir"/usr/lib/lib{LLVM,LTO,Remarks}.so

  # prepare llvm-libs dir in srcdir to store llvm runtime libraries files that would be part of llvm-libs-minimal-git package
  mkdir -p "$srcdir"/llvm-libs && mv -f "$pkgdir"/usr/lib/{libLLVM*.so*,libLTO.so.*,libRemarks.so.*} "$srcdir"/llvm-libs

  # prepare clang dir to store clang-minimal-git package files
  mkdir -p "$srcdir"/clang/{bin,lib/{cmake,},include,share}

  mv -f "$pkgdir"/usr/lib/{libear,libscanbuild,clang,libclang{,-cpp}.so*} "$srcdir"/clang/lib
  mv -f "$pkgdir"/usr/lib/cmake/clang "$srcdir"/clang/lib/cmake

  # Move analyzer scripts out of /usr/libexec
  mv -f "$pkgdir"/usr/libexec/* "$srcdir"/clang/lib/clang && rmdir "$pkgdir"/usr/libexec
  # clang includes
  mv -f "$pkgdir"/usr/include/{clang,clang-c} "$srcdir"/clang/include
  # clang binaries
  mv -f "$pkgdir"/usr/bin/{analyze-build,c-index-test,clang*,diagtool,find-all-symbols,git-clang-format,hmaptool,intercept-build,modularize,pp-trace,run-clang-tidy,scan-build,scan-build-py,scan-view} "$srcdir"/clang/bin
  # clang share
  mv -f "$pkgdir"/usr/share/{clang,man,opt-viewer,scan-build,scan-view} "$srcdir"/clang/share/

  install -Dm644 "$srcdir"/llvm-project/llvm/LICENSE.TXT "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE.TXT
}

package_llvm-libs-minimal-git() {
  pkgdesc="LLVM runtime libraries, trimmed down trunk version"
  depends=(llvm-libs zlib libffi libedit ncurses libxml2)
  # create $pkgdir/usr/lib and copy content from llvm-libs/* into it
  install -d "$pkgdir"/usr/lib
  cp -a "$srcdir"/llvm-libs/* "$pkgdir"/usr/lib

  install -Dm644 "$srcdir"/llvm-project/llvm/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.TXT
}

_python_optimize() {
  python -m compileall "$@"
  python -O -m compileall "$@"
  python -OO -m compileall "$@"
}

# TODO: if install size increases drastically add back the find cmd that deletes static libraries
package_clang-minimal-git() {
  pkgdesc='C language family frontend for LLVM (trimmed down git version)'
  depends=(llvm-libs-minimal-git clang)
  optdepends=('openmp: OpenMP support in clang with -fopenmp'
    'python: for scan-view, scan-build, git-clang-format, clang-rename and python bindings'
    'llvm-minimal-git: referenced by some clang headers')
  conflicts=(compiler-rt clang)
  provides=(compiler-rt clang clang-analyzer clang-tools-extra)

  # clang libraries
  install -d "$pkgdir"/usr/lib
  cp -a $srcdir/clang/lib/{libear,libscanbuild,clang,cmake,libclang*} $pkgdir/usr/lib

  # clang includes
  install -d "$pkgdir"/usr/include
  cp -a "$srcdir"/clang/include/{clang,clang-c} "$pkgdir"/usr/include

  # clang binaries
  install -d "$pkgdir"/usr/bin
  cp -a "$srcdir"/clang/bin/{analyze-build,c-index-test,clang*,diagtool,find-all-symbols,git-clang-format,hmaptool,intercept-build,modularize,pp-trace,run-clang-tidy,scan-build,scan-build-py,scan-view} "$pkgdir"/usr/bin

  # clang share
  install -d "$pkgdir"/usr/share
  cp -a "$srcdir"/clang/share/{clang,man,opt-viewer,scan-build,scan-view} "$pkgdir"/usr/share

  # Move scanbuild-py into site-packages
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/$site_packages"
  mv "$pkgdir"/usr/lib/{libear,libscanbuild} "$pkgdir/$site_packages"

  sed -i 's|libexec|lib/clang|' \
    "$pkgdir/usr/bin/scan-build" \
    "$pkgdir/$site_packages/libscanbuild/analyze.py"

  # Compile Python scripts
  _python_optimize "$pkgdir"/usr/share "$pkgdir"/$site_packages

  install -Dm644 "$srcdir"/llvm-project/compiler-rt/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/compiler-rt-LICENSE.TXT
  install -Dm644 "$srcdir"/llvm-project/clang-tools-extra/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/clang-tools-extra-LICENSE.TXT
  install -Dm644 "$srcdir"/llvm-project/clang/LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/clang-LICENSE.TXT
}
